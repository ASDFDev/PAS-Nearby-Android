apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'me.tatarka.retrolambda'
apply from: "$project.rootDir/tools/script-pmd.gradle"

def keystorePropertiesFile = rootProject.file("gradle.properties");
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.1"
    defaultConfig {
        applicationId "org.sp.attendance"
        minSdkVersion 19
        targetSdkVersion 26
        versionCode 38
        versionName "3.8"
        vectorDrawables.useSupportLibrary = true
    }

    signingConfigs {
        release {
            storeFile file(keystoreProperties['keystoreFile'])
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }

    buildTypes {
        debug {
            resValue 'string', 'GoogleAPIKey', DebugAppKey
            versionNameSuffix "-debug"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue 'string', 'GoogleAPIKey', ReleaseAppKey
            versionNameSuffix "-release"
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dataBinding {
        enabled = true
    }

    defaultConfig {
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }

}

dependencies {

    ext.support_version = '26.0.1'
    ext.play_services_version = '11.2.0'

    compile fileTree(include: ['*.jar'], dir: 'libs')
    // Kotlin
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    compile 'com.mcxiaoke.koi:core:0.5.4'

    // Android support library
    compile "com.android.support:appcompat-v7:$support_version"
    compile "com.android.support:design:$support_version"
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    // Google Play services library
    compile "com.google.android.gms:play-services-nearby:$play_services_version"
    // Firebase
    compile "com.google.firebase:firebase-database:$play_services_version"

    // UX enhancement
    compile 'com.github.ybq:Android-SpinKit:1.1.0'
    compile 'com.github.deano2390:MaterialShowcaseView:1.1.0'
    compile 'com.heinrichreimersoftware:material-intro:1.6.2'

    compile 'org.jsoup:jsoup:1.10.3'
    compile 'com.google.code.gson:gson:2.8.0'

    // Testing
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'
}
apply plugin: 'com.google.gms.google-services'